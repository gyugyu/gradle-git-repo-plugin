apply plugin: 'groovy'
apply plugin: 'maven'

sourceCompatibility = 1.7
group = 'com.layer'
apply from: "$rootDir/version.gradle"

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'org.ajoberstar:gradle-git:1.7.2'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: hasProperty('ossrhUsername')?ossrhUsername:'', password: hasProperty('ossrhPassword')?ossrhPassword:'')
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: hasProperty('ossrhUsername')?ossrhUsername:'', password: hasProperty('ossrhPassword')?ossrhPassword:'')
            }

            pom.project {
                name 'Gradle Git Repo Plugin'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Use a git repo as a maven-style repo'
                url 'https://github.com/layerhq/gradle-git-repo-plugin'

                scm {
                    connection 'scm:git:git@github.com:layerhq/gradle-git-repo-plugin.git'
                    developerConnection 'scm:git:git@github.com:layerhq/gradle-git-repo-plugin.git'
                    url 'https://github.com/layerhq/gradle-git-repo-plugin'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'drapp'
                        name 'Douglas Rapp'
                        email 'doug@layer.com'
                    }
                }
            }
        }
    }
}

